plugins {
  id 'java-library'
  id "io.spring.dependency-management" version "1.1.5"
  id "org.springframework.boot" version "3.3.4"
  id 'maven-publish'
  id 'org.jreleaser' version '1.20.0'
}

group 'no.datek'
version '1.1.9.4'
final String JRUBY_VERSION = '10.0.2.0';

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation "org.jruby:jruby:$JRUBY_VERSION"
}

java {
  withJavadocJar()
  withSourcesJar()
}

test {
  useJUnitPlatform()
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId = 'no.datek'
      artifactId = 'slim-spring-webmvc'
      from components.java

      pom {
        name = 'slim-spring-webmvc'
        description = 'Add https://github.com/slim-template/slim as a template engine to Spring WebMVC.'
        url = 'https://github.com/DatekWireless/slim-spring-webmvc'
        inceptionYear = '2023'
        licenses {
          license {
            name = 'MIT License'
            url = 'https://mit-license.org/'
          }
        }

        scm {
          url = 'https://github.com/DatekWireless/slim-spring-webmvc'
          connection = 'scm:git://github.com:DatekWireless/slim-spring-webmvc.git'
          developerConnection = 'scm:git://github.com:DatekWireless/slim-spring-webmvc.git'
        }

        developers {
          developer {
            id = 'UweKubosch'
            name = 'Uwe Kubosch'
            email = 'uwe@datek.no'
            organizationUrl = 'https://datek.no/'
          }
        }
      }
    }
  }
  repositories {
    maven {
      url = layout.buildDirectory.dir('staging-deploy')
    }
  }
}


jreleaser {
  signing {
    active = 'ALWAYS'
    armored = true
  }
  deploy {
    maven {
      mavenCentral {
        sonatype {
          active = 'ALWAYS'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository('build/staging-deploy')
        }
      }
    }
  }
}
